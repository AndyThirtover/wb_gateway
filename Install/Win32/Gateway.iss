; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!
[Setup]
#define PY_PATH  "Python25"
#define PY_VER  "2.5"
#define PY_VER_MINOR  "2.5.2"
#define TWISTED_VER "8.1.0"

; need updating as version of each egg changes
#define LIB_VER  "2.0"
#define RES_VER  "2.0"
#define CFG_VER  "2.0"
#define GATEWAY_VER  "2.0"
#define DOC_VER  "2.0"

#define SETUPTOOLS_VER  "0.6c7"

#define LIB_NAME  "WebBrickLibs"
#define RES_NAME  "WebBrickRes"
#define CFG_NAME  "WebBrickConfig"
#define GATEWAY_NAME  "WebBrickGateway"
#define DOC_NAME  "WebBrickDoc"


; Location of files
; pain cannot use define in define
#define VCRUNTIME  "..\..\..\..\..\vcruntime\"
#define PYTHONFILES  "..\..\..\..\..\Python\Python2.5\"
#define TGFILES  "..\..\..\..\..\Turbogears\Python2.5_tg1.0.4.4\"
; used for deleting old versions from system path
#define PYTHONINSTALLOLD  "{pf}\Python24"
#define PYTHONINSTALLLOC  "{pf}\Python25"
#define PYTHONINTERPRETER  "{pf}\Python25\python.exe"
#define SCRIPTDIR  "{pf}\Python25\scripts"
#define EASYINSTALL  "{pf}\Python25\scripts\easy_install.exe"

AppName=WebBrick Gateway
AppVerName=WebBrick Gateway 2.0
AppPublisher=WebBrickSystems Ltd.
AppPublisherURL=http://www.WebBrickSystems.com/
AppSupportURL=http://community.WebBrickSystems.com/
AppUpdatesURL=http://www.WebBrickSystems.com/
DefaultDirName={pf}\WebBrickSystems\Webbrick Gateway
DefaultGroupName=WebBrick Gateway
AllowNoIcons=true
LicenseFile=..\..\license.txt
InfoBeforeFile=..\..\install.txt
InfoAfterFile=..\..\postinstall.txt
OutputBaseFilename=setup
Compression=zip/9
ChangesEnvironment=yes
SolidCompression=true
; run as admin
PrivilegesRequired=admin
VersionInfoVersion=1.1
VersionInfoCompany=WebBrickSystems
VersionInfoDescription=WebBrick Gateway
VersionInfoTextVersion=WebBrick Gateway
OutputManifestFile=setup.lst
VersionInfoCopyright=(C) L.P.Klyne 2013
ShowLanguageDialog=auto

[Registry]
Root: HKLM; Subkey: Software\WebBrickSystems\WebBrick Gateway; Flags: uninsdeletekeyifempty; ValueType: string; ValueName: InstallLoc; ValueData: {app}

[Dirs]
Name: {commonappdata}\webbrick; Permissions: everyone-modify
Name: {commonappdata}\webbrick\gateway; Permissions: everyone-modify
; log file directory
Name: {commonappdata}\webbrick\gateway\logs; Permissions: everyone-modify
; site directory
Name: {commonappdata}\webbrick\gateway\site; Permissions: everyone-modify

[Languages]
Name: english; MessagesFile: compiler:Default.isl

[Tasks]
Name: PYTHON; Description: Python Runtime
Name: PYTHONEXT; Description: Python WIN32 Extensions
Name: PREREQUISITES; Description: Prerequisites for Turbogears and Gateway
Name: TURBOGEARS; Description: Turbogears Runtime
Name: GATEWAY; Description: WebBrick Gateway
Name: GATEWAYSERVICE; Description: Install WebBrick Gateway as Windows Service; Flags: checkablealone
Name: GATEWAYSERVICE\AUTO; Description: auto start WebBrick Gateway service.; Languages: 

[Files]
Source: {#PYTHONFILES}python-{#PY_VER_MINOR}.msi; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PYTHON
Source: {#VCRUNTIME}*.*; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}mfc71.dll; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71CHS.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71CHT.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71DEU.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71ENU.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71ESP.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71FRA.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71ITA.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71JPN.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}MFC71KOR.DLL; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
;Source: {#VCRUNTIME}mfc71u.dll; DestDir: {#PYTHONINSTALLLOC}; Tasks: PYTHONEXT
Source: {#PYTHONFILES}*.*; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PYTHONEXT
;Source: {#PYTHONFILES}pywin32-210.win32-py{#PY_VER}.exe; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PYTHONEXT
;Source: {#PYTHONFILES}Twisted_NoDocs-{#TWISTED_VER}.win32-py{#PY_VER}.exe; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PREREQUISITES
;Source: {#PYTHONFILES}pyserial-2.2.zip; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PREREQUISITES
;Source: {#PYTHONFILES}Louie-1.1.tar.gz; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PREREQUISITES
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
;Source: {#PYTHONFILES}ez_setup.py; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PREREQUISITES
;Source: {#PYTHONFILES}setuptools-{#SETUPTOOLS_VER}-py{#PY_VER}.egg; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: PREREQUISITES
;Source: {#TGFILES}tgsetup.py; DestDir: {tmp}; Flags: deleteafterinstall nocompression; Tasks: TURBOGEARS
Source: {#TGFILES}*.*; DestDir: {tmp}\TurboGears; Flags: deleteafterinstall nocompression; Tasks: TURBOGEARS
Source: ..\..\{#LIB_NAME}\dist\*{#PY_VER}.egg; DestDir: {tmp}\Gateway; Flags: deleteafterinstall nocompression; Tasks: GATEWAY
Source: ..\..\{#RES_NAME}\dist\*{#PY_VER}.egg; DestDir: {tmp}\Gateway; Flags: deleteafterinstall nocompression; Tasks: GATEWAY
Source: ..\..\{#GATEWAY_NAME}\dist\*{#PY_VER}.egg; DestDir: {tmp}\Gateway; Flags: deleteafterinstall nocompression; Tasks: GATEWAY
Source: ..\..\{#DOC_NAME}\dist\*{#PY_VER}.egg; DestDir: {tmp}\Gateway; Flags: deleteafterinstall nocompression; Tasks: GATEWAY
Source: ..\..\{#CFG_NAME}\dist\*{#PY_VER}.egg; DestDir: {tmp}\Gateway; Flags: deleteafterinstall nocompression; Tasks: GATEWAY

[Icons]
; Update python shortcuts and icons.      with paramter ; Tasks: PYTHON
Name: {group}\My WebBrick Gateway; Filename: http://localhost:8080/
Name: {group}\WebBrick on the Web; Filename: http://www.WebBrickSystems.com/
Name: {group}\WebBrick documentation; Filename: {#SCRIPTDIR}/webbrick_doc.exe
Name: {group}\Configuration Files; Filename: {commonappdata}\webbrick\gateway

[Run]
; START Python
; Visual Studio 2003 Runtime stuff
;Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\VCCRT71.msi"" TARGETDIR=""{sys}"""; Tasks: PYTHON;
;Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\VCMFC71.msi"" TARGETDIR=""{sys}"""; Tasks: PYTHON;
;Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\VCMFC71Loc.msi"" TARGETDIR=""{sys}"""; Tasks: PYTHON;
; Will need to fixup some shortcuts.
;http://www.python.org/download/releases/2.4/msi/
Filename: msiexec.exe; Parameters: "/i ""{tmp}\python-{#PY_VER_MINOR}.msi"" TARGETDIR=""{#PYTHONINSTALLLOC}"" ALLUSERS=1 /qn ADDLOCAL=ALL"; Tasks: PYTHON; AfterInstall: UpdatePythonPaths()

Filename: {tmp}\pywin32-210.win32-py{#PY_VER}.exe; Parameters: " /SP- /SILENT"; WorkingDir: {tmp}; Flags: skipifdoesntexist; Tasks: PYTHONEXT
Filename: {#PYTHONINTERPRETER}; Parameters: "{tmp}\ez_setup.py -H None -f ""{tmp}"" "; WorkingDir: {tmp}; Tasks: PREREQUISITES

Filename: {tmp}\Twisted_NoDocs-{#TWISTED_VER}.win32-py{#PY_VER}.exe; Parameters: " /SP- /SILENT"; WorkingDir: {tmp}; Flags: skipifdoesntexist; Tasks: PREREQUISITES

Filename: {#EASYINSTALL}; Parameters: " -H None -f ""{tmp}"" netifaces-0.4-py2.5-win32.egg "; WorkingDir: {tmp}; Flags: ; Tasks: PREREQUISITES
Filename: {#EASYINSTALL}; Parameters: " -H None -f ""{tmp}"" Louie-1.1.tar.gz "; WorkingDir: {tmp}; Flags: ; Tasks: PREREQUISITES
Filename: {#EASYINSTALL}; Parameters: " -H None -f ""{tmp}"" pyserial-2.2.zip "; WorkingDir: {tmp}; Flags: ; Tasks: PREREQUISITES

Filename: {#EASYINSTALL}; Parameters: " -H None -f ""{tmp}\TurboGears"" TurboGears"; WorkingDir: {tmp}\TurboGears; Flags: ; Tasks: TURBOGEARS

Filename: {#EASYINSTALL}; Parameters: " --no-deps -H None -f ""{tmp}\Gateway"" {#LIB_NAME}-{#LIB_VER}-py{#PY_VER}.egg"; WorkingDir: {tmp}\Gateway; Flags: ; Tasks: GATEWAY
Filename: {#EASYINSTALL}; Parameters: " --no-deps -H None -f ""{tmp}\Gateway"" {#RES_NAME}-{#RES_VER}-py{#PY_VER}.egg"; WorkingDir: {tmp}\Gateway; Flags: ; Tasks: GATEWAY
Filename: {#EASYINSTALL}; Parameters: " --no-deps -H None -f ""{tmp}\Gateway"" {#CFG_NAME}-{#CFG_VER}-py{#PY_VER}.egg"; WorkingDir: {tmp}\Gateway; Flags: ; Tasks: GATEWAY
Filename: {#EASYINSTALL}; Parameters: " --no-deps -H None -f ""{tmp}\Gateway"" {#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg"; WorkingDir: {tmp}\Gateway; Flags: ; Tasks: GATEWAY; AfterInstall: UpdateLogPath(ExpandConstant('{commonappdata}\webbrick\gateway\logs\'))
Filename: {#PYTHONINTERPRETER}; Parameters: " ""{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\webbrick_util.pyc"" --new --directory ""{commonappdata}\webbrick\gateway\site"" "; WorkingDir: {tmp}; Tasks: GATEWAY
Filename: {#EASYINSTALL}; Parameters: " --no-deps -H None -f ""{tmp}\Gateway"" {#DOC_NAME}-{#DOC_VER}-py{#PY_VER}.egg"; WorkingDir: {tmp}\Gateway; Flags: ; Tasks: GATEWAY; AfterInstall: UpdateResPaths()
Filename: {#PYTHONINTERPRETER}; Parameters: " ""{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\GatewayService.pyc"" ""-c {commonappdata}\webbrick\gateway\site\prod.cfg"" install"; WorkingDir: {tmp}; Tasks: GATEWAYSERVICE
;Filename: {#PYTHONINTERPRETER}; Parameters: " ""{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\GatewayService.pyc"" ""-c {#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\resources\prod.cfg"" install"; WorkingDir: {tmp}; Tasks: GATEWAYSERVICE
Filename: {#PYTHONINTERPRETER}; Parameters: " ""{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\GatewayService.pyc"" ""--startup=auto"" update"; WorkingDir: {tmp}; Tasks: GATEWAYSERVICE\AUTO
Filename: {#PYTHONINTERPRETER}; Parameters: " ""{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\GatewayService.pyc"" start"; WorkingDir: {tmp}; Tasks: GATEWAYSERVICE\AUTO

[Code]
#include "modpath.iss"
function GetPythonPath(): String;
var
	pypath:	String;
begin
    // read registry
    RegQueryStringValue(HKEY_LOCAL_MACHINE, '\SOFTWARE\Python\PythonCore\2.5\InstallPath', '', pypath);
    Result := pypath;
end;

procedure UpdatePythonPaths();
var
  pathroot:String;
begin
  pathroot := ExpandConstant('{#PYTHONINSTALLLOC}');
  RemovePath( ExpandConstant('{#PYTHONINSTALLOLD}') );
  AddPath( pathroot );
  AddPath( pathroot+'\scripts' );
end;

procedure UpdateLogPath( logroot:String );
var
  fname: String;
  root: String;
begin
  // INI section runs earlier than RUN in install.
  fname := ExpandConstant('{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\config\log.cfg');
  root := '''' + logroot +'''';
  // Python/configobj expects Unix path separator as backslash is string escape
  StringChangeEx( root, '\', '/', True );
  Log('update ' + fname + ' ' + root);
  SetIniString( 'DEFAULT', 'logRoot', root, fname );
  Log('Done ' + root);
end;

procedure UpdateResPaths();
var
  fname: String;
  npath: String;
begin

  // hence update afterinstall
  // update static root and doc root
  fname := ExpandConstant('{#PYTHONINSTALLLOC}\lib\site-packages\{#GATEWAY_NAME}-{#GATEWAY_VER}-py{#PY_VER}.egg\{#GATEWAY_NAME}\config\app.cfg');

  npath := '"'+ExpandConstant('{#PYTHONINSTALLLOC}/lib/site-packages/{#RES_NAME}-{#RES_VER}-py{#PY_VER}.egg/resources/static/')+'"';
  // Python/configobj expects Unix path separator as backslash is string escape
  StringChangeEx( npath, '\', '/', True );
  Log('update ' + fname + ' ' + npath);
  SetIniString( 'global', 'static_filter.root', npath, fname );
  Log('Done ' + npath);

  npath := '"'+ExpandConstant('{#PYTHONINSTALLLOC}/lib/site-packages/{#DOC_NAME}-{#DOC_VER}-py{#PY_VER}.egg/')+'"';
  StringChangeEx( npath, '\', '/', True );
  Log('update ' + fname + ' ' + npath);
  SetIniString( '/doc', 'static_filter.dir', npath, fname );
  Log('Done ' + npath);
end;

[INI]

